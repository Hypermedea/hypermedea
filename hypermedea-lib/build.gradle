/*
 * Build file for Hypermedea as a software library (a set of CArtAgO components)
 *
 */
plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

version '0.4.0'
group   'org.hypermedea'

java {
    toolchain {
        // ensures compatibility with JaCaMo 1.2
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    // artifact repository for JaCaMo-related project
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://raw.github.com/jacamo-lang/mvn-repo/master' }
    maven { url "https://jitpack.io" }
}

dependencies {
    compileOnly group: 'org.jacamo', name: 'jacamo', version: '1.2'

    testImplementation group: 'org.jacamo', name: 'jacamo', version: '1.2'
    testImplementation 'junit:junit:4.13.1'

    // RDF dependencies
    implementation group: 'org.apache.jena', name: 'jena-core', version: '3.16.0'
    implementation group: 'org.apache.jena', name: 'jena-arq', version: '3.16.0'
    implementation group: 'org.apache.jena', name: 'jena-iri', version: '3.16.0'

    // HTTP dependencies
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'hypermedea'
            version = version

            from components.java
        }
    }
}

/**
 * task to create a all-in-one jar that can be used as JaCaMo library
 */
task jacamoJar(type: Jar) {
    archiveClassifier = 'standalone'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

clean {
    delete 'bin'
    delete 'build'
    delete 'log'
}
