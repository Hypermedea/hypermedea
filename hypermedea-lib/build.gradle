/*
 * Build file for Hypermedea as a software library (a set of CArtAgO components)
 *
 */
plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

version '0.2'
group   'org.hypermedea'

repositories {
    mavenCentral()
    // artifact repository for JaCaMo-related project
    maven { url 'https://raw.github.com/jacamo-lang/mvn-repo/master' }
    maven { url "https://jitpack.io" }
}

dependencies {
    compileOnly group: 'org.jacamo', name: 'jacamo', version: '0.9'

    testImplementation group: 'org.jacamo', name: 'jacamo', version: '0.9'
    testImplementation 'junit:junit:4.13.1'

    // Linked Data (LD) and Web Ontology Language (OWL) dependencies
    implementation group: 'com.github.owlcs', name: 'ontapi', version: '2.1.0'
    implementation group: 'net.sourceforge.owlapi', name: 'org.semanticweb.hermit', version: '1.4.5.519'

    // Thing Description (TD) client dependencies
    implementation group: 'com.github.Hypermedea', name: 'wot-td-java', version: '1f08cce'

    // Planning Domain Description Language (PDDL) dependencies
    implementation group: 'com.github.pellierd', name: 'pddl4j', version: 'v3.8.3'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.hypermedea'
            artifactId = 'hypermedea'
            version = version

            from components.java
        }
    }
}

/**
 * task to create a all-in-one jar that can be used as JaCaMo library
 */
task jacamoJar(type: Jar) {
    archiveClassifier = 'standalone'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

clean {
    delete 'bin'
    delete 'build'
    delete 'log'
}
