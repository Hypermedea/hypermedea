/*
 * Hypermedea build file
 *
 */

plugins {
    id 'java'
    id 'application'
}

version '0.1'
group   'org.hypermedea'

repositories {
    mavenCentral()
    // artifact repository for JaCaMo-related project
    maven { url 'https://raw.github.com/jacamo-lang/mvn-repo/master' }
    // artifact repository for Linked-Data-Fu
    maven { url 'https://paul.ti.rw.fau.de/maven/repository/' }
}

dependencies {
    implementation group: 'edu.kit.aifb.linkeddatafu', name: 'linked-data-fu-standalone', version: '0.9.12'
    implementation group: 'org.apache.jena', name: 'apache-jena-libs', version: '4.1.0'
    implementation group: 'net.sourceforge.owlapi', name: 'org.semanticweb.hermit', version: '1.4.5.519'
    // TODO include newer version of wot-td-java
    implementation group: 'ch.unisg.ics.interactions', name: 'wot-td-java', version: '0.0.1'
    implementation fileTree(dir: 'lib', include: 'pddl4j-*.jar')

    testImplementation 'junit:junit:4.13.1'

    // JaCaMo is used for compiling artifacts and then for distributing a full application
    implementation group: 'org.jacamo', name: 'jacamo', version: '0.9'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

mainClassName = 'jacamo.infra.JaCaMoLauncher'

/**
 * modified jar task to include all dependencies to the produced artifact,
 * to then be added as lib of the standard JaCaMo distribution
 */
//task libJar(type: Jar) {
//    archiveAppendix = 'lib'
//    from {
//        // TODO filter out jacamo and dependencies
//        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//    }
//    with jar
//}

/**
 * integration tests of artifacts with Jason agents
 */
task testAgt (type: JavaExec, dependsOn: 'classes') {
    description 'runs the JaCaMo application'
    doFirst {
        mkdir 'log'
    }
    main = 'jacamo.infra.JaCaMoLauncher'
    args 'test.jcm'
    workingDir = 'src/test/jacamo/'
    classpath sourceSets.main.runtimeClasspath
}

clean {
    delete 'bin'
    delete 'build'
    delete 'log'
}
