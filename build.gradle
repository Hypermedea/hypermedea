/*
 * Hypermedea build file
 *
 */

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

version '0.1'
group   'org.hypermedea'

mainClassName = 'jacamo.infra.JaCaMoLauncher'

repositories {
    mavenCentral()
    // artifact repository for JaCaMo-related project
    maven { url 'https://raw.github.com/jacamo-lang/mvn-repo/master' }
    // artifact repository for Linked-Data-Fu
    maven { url 'https://paul.ti.rw.fau.de/maven/repository/' }
}

dependencies {
    implementation 'junit:junit:4.13.1'
    compile group: 'org.jacamo', name: 'jacamo', version: '0.9'
    compile group: 'ch.unisg.ics.interactions', name: 'wot-td-java', version: '0.0.1'
    compile group: 'edu.kit.aifb.linkeddatafu', name: 'linked-data-fu-standalone', version: '0.9.12'
    compile group: 'net.sourceforge.owlapi', name: 'owlapi-apibinding', version: '5.1.17'
    compile group: 'com.hermit-reasoner', name: 'org.semanticweb.hermit', version: '1.3.8.1'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

publishing {
    publications {
        lib(MavenPublication) {
            from components.java
        }
    }

    // TODO add Maven Central repo with auth
    // see https://docs.gradle.org/4.4.1/userguide/publishing_maven.html
}

/**
 * integration tests of artifacts with Jason agents
 */
task testAgt (type: JavaExec, dependsOn: 'classes') {
    description 'runs the JaCaMo application'
    doFirst {
        mkdir 'log'
    }
    main = 'jacamo.infra.JaCaMoLauncher'
    args 'test.jcm'
    workingDir = 'src/test/jacamo/'
    classpath sourceSets.main.runtimeClasspath
}

// Clean up
clean {
    delete 'bin'
    delete 'build'
    delete 'log'
}
